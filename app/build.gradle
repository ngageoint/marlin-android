import org.ajoberstar.grgit.Grgit

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'com.google.protobuf'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

group 'mil.nga.msi'
version '1.2.0'

def googleMapsApiReleaseKey = hasProperty('MSI_RELEASE_MAPS_API_KEY') ? MSI_RELEASE_MAPS_API_KEY : ''
def googleMapsApiDebugKey = hasProperty('MSI_DEBUG_MAPS_API_KEY') ? MSI_DEBUG_MAPS_API_KEY : ''

def room_version = '2.5.1'
def work_version = '2.8.1'
def compose_version = '1.4.3'
def retrofit_version = '2.9.0'
def accompanist_version = '0.30.1'

android {
    compileSdk 33
    namespace 'mil.nga.msi'

    defaultConfig {
        ext.repo = Grgit.open(currentDir: project.rootDir)

        applicationId "mil.nga.msi"
        minSdk 26
        targetSdk 33
        versionName project.version
        versionCode ext.repo.log().size() as int

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        signingConfigs {
            release {
                if (project.hasProperty('MSI_KEYSTORE')) {
                    storeFile file(MSI_KEYSTORE)
                    storePassword MSI_KEYSTORE_PASSWORD
                    keyAlias MSI_KEY_ALIAS
                    keyPassword MSI_KEY_PASSWORD
                }
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiDebugKey
            resValue "string", "applicationId", defaultConfig.applicationId + applicationIdSuffix
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiReleaseKey
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    kotlin {
        jvmToolchain(11)
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.3"
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.23.0'
        }

        // Generates the java Protobuf-lite code for the Protobufs in this project. See
        // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
        // for more information.
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option 'lite'
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            assets {
                srcDirs 'src/main/assets'
            }
        }
        androidTest {
            assets {
                srcDirs 'src/androidTest/assets'
            }
        }
    }
}

dependencies {
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    annotationProcessor 'androidx.hilt:hilt-compiler:1.0.0'

    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:2.5.1"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "com.tickaroo.tikxml:annotation:0.8.13"
    implementation "com.tickaroo.tikxml:core:0.8.13"
    kapt "com.tickaroo.tikxml:processor:0.8.13"
    implementation "com.tickaroo.tikxml:retrofit-converter:0.8.13"

    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation "androidx.startup:startup-runtime:1.1.1"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:1.1.0"
    implementation 'androidx.compose.material:material:1.4.3'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:1.5.0-alpha04"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation "androidx.lifecycle:lifecycle-service:2.6.1"
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation('androidx.paging:paging-runtime-ktx:3.1.1')
    implementation("androidx.paging:paging-compose:1.0.0-alpha19")
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    implementation 'com.google.protobuf:protobuf-javalite:3.23.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    implementation 'com.google.maps.android:maps-compose:2.11.4'
    implementation 'com.google.maps.android:maps-ktx:3.3.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'mil.nga.gars:gars-android:1.2.2'
    implementation 'mil.nga.mgrs:mgrs-android:2.2.2'

    implementation("io.coil-kt:coil-compose:2.2.2")

    implementation 'com.google.android.gms:play-services-location:21.0.1'

    implementation 'mil.nga.geopackage:geopackage-android:6.7.1'
    implementation 'mil.nga.geopackage.map:geopackage-android-map:6.7.1'

    implementation('edu.stanford.nlp:stanford-corenlp:4.5.4') {
        exclude group: 'org.hamcrest'
        exclude group: 'com.sun.xml.bind'
        exclude group: 'com.google.protobuf'
    }

    implementation 'com.github.matomo-org:matomo-sdk-android:4.1.4'

    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"

    implementation("com.google.android.gms:play-services-oss-licenses:17.0.1")

    testImplementation libs.junit4
    testImplementation project(':testing')
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation project(':testing')
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    testImplementation "io.mockk:mockk:1.4.1"

    androidTestImplementation 'org.mockito:mockito-core:5.3.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.3'
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    androidTestImplementation "io.mockk:mockk:1.4.1"
}